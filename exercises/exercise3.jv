pipeline GoodsPipeline {
    GoodsExtractor
    -> GoodsTextFileInterpreter
    -> GoodsTextRangeSelector
    -> GoodsCSVInterpreter
    -> GoodsHeaderName_AtoE
    -> GoodsHeaderName_ATtoAU
    -> GoodsTableInterpreter
    -> GoodsLoader;
    
    block GoodsTextFileInterpreter oftype TextFileInterpreter {
        encoding: "latin2"; 
    }

    block GoodsExtractor oftype HttpExtractor {
        url: "https://www-genesis.destatis.de/genesis/downloads/00/tables/46131-0014_00.csv";   
    }

    block GoodsTextRangeSelector oftype TextRangeSelector { 
        lineFrom: 9; lineTo: 41336; 
    }

    
    block GoodsCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    block GoodsHeaderName_AtoE oftype CellWriter { 
        at: range A1:E1; write: ["year", "month", "goods_id", "goods_name", "goods_source"]; 
    }
    block GoodsHeaderName_ATtoAU oftype CellWriter { 
        at: range AT1:AU1; write: ["abroad", "total"];
    }

    constraint GoodsIdFormat oftype RegexConstraint {
        regex: /^NST7-[A-Z0-9]{3}$/;
    }

    valuetype GoodsId oftype text { 
        constraints: [GoodsIdFormat]; 
    }
    
    
    constraint PositiveRange oftype RangeConstraint { 
        lowerBound: 0; lowerBoundInclusive: false;
    }
    valuetype PositiveInteger oftype integer { 
        constraints: [ PositiveRange ]; 
    }

    block GoodsTableInterpreter oftype TableInterpreter {
        header: true;
        columns:[
            "year" oftype PositiveInteger,
            "month" oftype text,
            "goods_id" oftype GoodsId,
            "goods_name" oftype text,
            "goods_source" oftype text,
            "abroad" oftype PositiveInteger,
            "total" oftype PositiveInteger,
        ];
    } 

    block GoodsLoader oftype SQLiteLoader {
        table: "goods";
        file: "./goodsTransportedByTrain.sqlite";
    }

}